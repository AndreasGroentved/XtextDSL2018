grammar dk.sdu.WPage with org.eclipse.xtext.common.Terminals

generate wPage "http://www.sdu.dk/WPage"

Contents: 
	contents+= Content*
;

Content:
	Variable | Page
;

Page: 
	'page' name=STRING '{' pagecontents += PageContent* '}'
;

PageContent: 
	Title | Css | Variable | External | Include | GroupedView | IfElse
;

Title: 
	'title' value=STRING
;

Css: 
	'css' value=STRING
;

GroupedView: 
	group+=AdvancedType ('+' group+=AdvancedType)*
;

Variable: 
	'var' name=ID (value=Type | value=AdvancedType)
;

Type: 
	Text | Number | Boolean
;

Text returns Type: 
	{Text} 'text' value=STRING 
;

Number returns Type: 
	{Number} 'number' value=ID
;

Boolean returns Type: 
	{Boolean} 'bool' value=ID
;

AdvancedType: 
	View | Button
;

View returns AdvancedType: 
	{View} 'view' '{' contents+= ViewContent*  '}' 
;

Button returns AdvancedType: 
	{Button} 'button' '{' contents += ButtonContent*  '}'
;

ViewConfiguration: 
	Dimension | Style
;

Dimension returns ViewConfiguration: 
	{Dimension} 'dimensions' values+=DimensionValue+
;

DimensionValue:
	Height | Width
;

Height returns DimensionValue:
	{Height} 'height' value=STRING
;

Width returns DimensionValue:
	{Width} 'width' value=STRING
;

Style returns ViewConfiguration: 
	{Style} 'style' styleDefinitions+=StyleDefinition+
;

StyleDefinition: 
	'horizontal' | 'vertical' | 'bold' | 'italic' | 'underline'
;

CssConfiguration:
	CssClass | CssId
;

CssClass returns CssConfiguration: 
	{CssClass} 'cssclass' value=STRING
;

CssId returns CssConfiguration: 
	{CssId} 'cssid' value=STRING
;

ButtonContent: 
	Click | ViewConfiguration| CssConfiguration | Text | 'text' include=Include
;

Click: 
	'click' '->'  (IfElse | Action)
;

IfElse: 
	If '{' ifAction = Action '}' ( 'else' '{' elseAction = Action '}')?
;

If: 
	'if' ifCondition=BooleanCondition ( op+=AndOr andOrConditions+=BooleanCondition)*  
;

AndOr: 
	'AND' | 'OR'
;

Action: 
	action=Next | action=First | Alert | Script | AdvancedType 
;

Next:
	'next'
;

First:
	'First'
;

External: 
	'external' script=Script
;

Script: 
	name=ID '(' ((parameters+=Value)','?)*  ')'
;

Alert: 
	'alert' value=STRING 
;

Editable:
	'editable' editable=STRING
;

ViewContent: 
	ViewConfiguration | CssConfiguration | Text | Image | Table | Include | Editable
;

Image: 
	'image' value=STRING 
;

BooleanCondition: 
	left=Operand operator=EqualityOperator right=Operand | Script
;

Operand: 
	ID | INT
;

EqualityOperator: 
	'==' | '!=' | '<' | '>' | '<=' | '>=' 
;

Table: 
	{Table} 'table' '{' tableContents += TableContent* '}'
;

TableContent: 
	ViewConfiguration | CssConfiguration | TableDimension | Header | Row
;

TableDimension: 
	'tabledimensions' value=STRING
;

Header: 
	'header' contents+=Value+
;

Row: 
	'row' contents+=Value+
;

Include: 
	'include' include=AdvancedType
;

Value: 
	val=STRING | Include
;
grammar dk.sdu.WPage with org.eclipse.xtext.common.Terminals

generate wPage "http://www.sdu.dk/WPage"

Contents: 
	contents+= Content*
;

Content:
	Variable | Page
;

Page: 
	'page' name=STRING '{' pagecontents += PageContent* '}'
;

PageContent: 
	Title | Css | Variable | External | Include | GroupedView | IfElse
;

Title: 
	'title' value=STRING
;

Css: 
	'css' value=STRING
;

GroupedView: 
	group+=AdvancedType ('+' group+=AdvancedType)*
;

Variable: 
	'var' (Type | AdvancedType)
;

Type: 
	Text | Number | Boolean
;

Text returns Type: 
	'text' name=ID value=STRING 
;

Number returns Type: 
	'number' name=ID value=ID
;

Boolean returns Type: 
	'bool' name=ID value=ID
;

AdvancedType: 
	TextView | View | Button
;

TextView returns AdvancedType: 
	'textview' name=ID '{' contents+= TextViewContent* '}'
;

View returns AdvancedType: 
	'view' name=ID '{' contents+= ViewContent*  '}' 
;

Button returns AdvancedType: 
	'button' name=ID '{' contents += ButtonContent*  '}'
;

ViewConfiguration: 
	Dimension | Style | CssClass | CssId
;

Dimension: 
	'dimensions' value=STRING
;

Style: 
	'style' styleDefinitions+=StyleDefinition+
;

StyleDefinition: 
	'horizontal' | 'vertical' | 'bold' | 'italic' | 'underline'
;

CssClass: 
	'cssclass' value=STRING
;

CssId: 
	'cssid' value=STRING
;

ButtonContent: 
	click=Click | viewConfig=ViewConfiguration | 'text' include=Include
;

Click: 
	'click' '->'  (IfElse | Action)
;

IfElse: 
	If '{' ifAction = Action '}' ( 'else' '{' elseAction = Action '}')?
;

If: 
	'if' ifCondition=BooleanCondition ( op+=AndOr andOrConditions+=BooleanCondition)*  
;

AndOr: 
	'AND' | 'OR'
;

Action: 
	action=Next | action=First | Alert | Script | AdvancedType 
;

Next:
	'next'
;

First:
	'First'
;

External: 
	'external' script=Script
;

Script: 
	name=ID '(' ((parameters+=Value)','?)*  ')'
;

Alert: 
	'alert' value=STRING 
;

TextViewContent: 
	ViewConfiguration | Text | Editable
;

Editable:
	'editable' editable=STRING
;

ViewContent: 
	ViewConfiguration | Text | Image | Table | Include
;

Image: 
	'image' value=STRING 
;

BooleanCondition: 
	left=Operand operator=EqualityOperator right=Operand | Script
;

Operand: 
	ID | INT
;

EqualityOperator: 
	'==' | '!=' | '<' | '>' | '<=' | '>=' 
;

Table: 
	'table' name=STRING '{' tableContents += TableContent* '}'
;

TableContent: 
	ViewConfiguration | TableDimension | Header | Row
;

TableDimension: 
	'tabledimensions' value=STRING
;

Header: 
	'header' contents+=Value+
;

Row: 
	'row' contents+=Value+
;

Include: 
	'include' include=AdvancedType
;

Value: 
	val=STRING | Include
;
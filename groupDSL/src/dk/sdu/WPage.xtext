grammar dk.sdu.WPage with org.eclipse.xtext.common.Terminals

generate wPage "http://www.sdu.dk/WPage"

Contents: 
	contents+= Content*
;

Content:
	Variable | Page
;

Page: 
	'page' name=STRING '{' pagecontents += PageContent* '}'
;

PageContent: 
	Title | Css | Variable | External | Include | GroupedView | IfElse 
;

Title: 
	'title' value=STRING
;

Css: 
	'css' value=STRING
;

GroupedView: 
	group+=AdvancedType ('+' group+=AdvancedType)*
;

Variable: 
	'var' name=ID (value=Type | value=AdvancedType)
;

Type: 	
	Text | Number | Boolean
;

Text returns Type: 
	{Text} 'text' value=STRING 
;

Number returns Type: 
	{Number} 'number' value= STRING
;

Boolean returns Type: 
	{Boolean} 'bool' value=ID
;

AdvancedType: 
	View | Button | Table | Form
;

View returns AdvancedType: 
	{View} 'view' '{' contents+= ViewContent*  '}' 
;

Button returns AdvancedType: 
	{Button} 'button' '{' contents += ButtonContent*  '}'
;



DisplayConfiguration:
	ViewConfiguration | CssConfiguration
;

ViewConfiguration: 
	Dimension | Style
;

Dimension returns ViewConfiguration: 
	{Dimension} 'dimensions' values+=DimensionValue+
;

DimensionValue:
	Height | Width
;

Height returns DimensionValue:
	{Height} 'height' value=STRING
;

Width returns DimensionValue:
	{Width} 'width' value=STRING
;

Style returns ViewConfiguration: 
	{Style} 'style' styleDefinitions+=StyleDefinition+
;

StyleDefinition: 
	'horizontal' | 'vertical' | 'bold' | 'italic' | 'underline'
;

CssConfiguration:
	CssClass | CssId
;

CssClass returns CssConfiguration: 
	{CssClass} 'cssclass' value=STRING
;

CssId returns CssConfiguration: 
	{CssId} 'cssid' value=STRING
;

ButtonContent: 
	Clicker | DisplayConfiguration | Text | 'text' include=Include
;



Clicker: 
	{Click} 'click' '->'  click = (IfElse |Action)
;

IfElse: 
	If '{' ifAction = Action '}' ( 'else' '{' elseAction = Action '}')?
;

If: 
	{If} ifCondition=BooleanCondition ( op+=AndOr andOrConditions+=BooleanCondition)*  
;

AndOr: 
	'AND' | 'OR'
;

Action: 
	 {Action} (pageDirection=PageDirection | alert=Alert | script=Script | advancedType=AdvancedType) 
;

PageDirection:
	'next' | 'previous' | 'first' | 'last'
;

External: 
	'external' script=Script
;

Script: 
	name=ID '(' ((parameters+=Value)','?)*  ')'
;

Alert: 
	'alert' value=STRING 
;

Editable:
	'editable' editable=STRING
;

ViewContent: 
	DisplayConfiguration | Text | Image | Table | Include | Editable 
;

Image: 
	'image' value=STRING 
;

Form:
	{Form} 'form' '{' contents += FormContent* '}'
;

FormContent:
	DisplayConfiguration | Text 
;



BooleanCondition: 
	left=Operand operator=EqualityOperator right=Operand | Script | Alert
;

Operand: 
	ID | INT
;

EqualityOperator: 
	'==' | '!=' | '<' | '>' | '<=' | '>=' 
;

Table: 
	{Table} 'table' '{' contents += TableContent* '}'
;

TableContent: 
	DisplayConfiguration | TableDimension | Header | Row 
;

TableDimension: 
	'tabledimensions' value=STRING
;

Header: 
	'header' contents+=Value+
;

Row: 
	'row' contents+=Value+
;


Include returns GroupedView: 
  {Include}	'include' include=[Variable]
;

Value: 
	value=STRING | Include
;

@Override
terminal SL_COMMENT 	: '#' !('\n'|'\r')* ('\r'? '\n')?;
//terminal FLOAT:  /*INT |('0'..'9')+('.'('0'..'9')*)?*/ STRING;
/*terminal CSSCSS: (STRING)'.css' STRING;*/
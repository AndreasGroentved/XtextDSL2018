grammar dk.sdu.WPage with org.eclipse.xtext.common.Terminals

generate wPage "http://www.sdu.dk/WPage"

Contents: 
	contents+= Content*
;

Content:
	Variable | Page
;

Page: 
	'page' name=STRING '{' pagecontents += PageContent* '}'
;

PageContent: 
	Title | Css | Variable | External | Include | GroupedView | IfElse 
;

Title: 
	'title' value=STRING
;

Css: 
	'css' value=STRING
;

GroupedView: 
	group+=AdvancedType ('+' group+=AdvancedType)*
;

Variable: 
	'var' name=ID (value=Type | value=AdvancedType)
;

Type: 	
	Text | Number | Boolean
;

Text returns Type: 
	{Text} 'text' value=TextValue 
;

Number returns Type: 
	{Number} 'number' (value=Whole | value=Decimal)
;

Whole:
	value = INT
;

Decimal:
	value = FLOAT
;

Boolean returns Type: 
	{Boolean} 'bool' value=ID
;

AdvancedType: 
	View | Button | Table 
;

View returns AdvancedType: 
	{View} 'view' '{' contents+= ViewContent*  '}' 
;

Button returns AdvancedType: 
	{Button} 'button' '{' contents += ButtonContent*  '}'
;

DisplayConfiguration:
	ViewConfiguration | CssConfiguration
;

ViewConfiguration: 
	Dimension | Style
;

Dimension returns ViewConfiguration: 
	{Dimension} 'dimensions' values+=DimensionValue+
;

DimensionValue:
	Height | Width
;

Height returns DimensionValue:
	{Height} 'height' value=STRING
;

Width returns DimensionValue:
	{Width} 'width' value=STRING
;

Style returns ViewConfiguration: 
	{Style} 'style' styleDefinitions+=StyleDefinition+
;

StyleDefinition: 
	'bold' | 'italic' | 'underline' | 'float' 
;

CssConfiguration:
	CssClass | CssId
;

CssClass returns CssConfiguration: 
	{CssClass} 'cssclass' value=STRING
;

CssId returns CssConfiguration: 
	{CssId} 'cssid' value=STRING
;

ButtonContent: 
	Clicker | DisplayConfiguration | Text
;

Clicker: 
	{Click} 'click' '->'  click = (IfElse |Action)
;

IfElse: 
	'if' condition=LogicExp '{' ifAction = Action '}' ( 'else' '{' elseAction = Action '}')?
;

LogicExp returns Proposition:
	Conjunction ('OR' {Disjunction.left=current} right=Conjunction)* 
;

Conjunction returns Proposition:
	Condition ('AND' {Conjunction.left=current} right=Condition)*
;

Condition returns Proposition:
	Comparison | Parenthesis
;

Comparison:
	left=Exp op=RelationalOp right=Exp
;

RelationalOp:
	'==' | '!=' | '<' | '>' | '<=' | '>=' 
;

Parenthesis:
	'(' condition=LogicExp ')'
;

Exp returns Expression:
	Factor ( ('+' {Add.left=current} | '-' {Sub.left=current}) right=Factor)*
;

Factor returns Expression:
	Primitive ( ('*' {Mul.left=current} | '/' {Div.left=current}) right=Primitive)*
;

Primitive returns Expression:
	Name | Number | bool=BooleanValue
;

Name: 
	name=[Variable]
;

BooleanValue:
	'true' | 'false'
;

Action: 
	 {Action} (pageDirection=PageDirection | alert=Alert | script=Script | advancedType=AdvancedType) 
;

PageDirection:
	'next' | 'previous' | 'first' | 'last'
;

External: 
	'external' script=Script
;

Script: 
	name=ID '(' ((parameters+=TextValue)','?)*  ')'
;

Alert: 
	'alert' value=STRING 
;

Editable:
	'editable' value=STRING
;

ViewContent: 
	DisplayConfiguration | Text | Image | Table | Include | Editable 
;

Image: 
	'image' value=STRING 
;

Table: 
	{Table} 'table' '{' contents += TableContent* '}'
;

TableContent: 
	DisplayConfiguration | TableDimension | Header | Row 
;

TableDimension: 
	'tabledimensions' value=STRING
;

Header: 
	'header' contents+=Value+
;

Row: 
	'row' contents+=Value+
;

Include returns GroupedView: 
  {Include}	'include' include=[Variable]
;

Value: 
	{Value} value=STRING | Include | variable=[Variable]
;

TextValue:
	{TextValue} string=STRING | variable=[Variable]
;

@Override
terminal SL_COMMENT 	: '#' !('\n'|'\r')* ('\r'? '\n')?;
terminal FLOAT:  INT'.'INT;
/*terminal CSSCSS: (STRING)'.css' STRING;*/
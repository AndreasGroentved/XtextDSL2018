grammar dk.sdu.WPage with org.eclipse.xtext.common.Terminals

generate wPage "http://www.sdu.dk/WPage"

Contents: 
	contents+= Content*
;

Content:
	Variable | Page
;

Page: 
	'page' name=STRING '{' pagecontents += PageContent* '}'
;

PageContent returns LayoutContent: 
	Title | Css | Variable | Include | GroupedView | IfElse 
;

Title: 
	'title' value=TextValue
;

Css: 
	'css' value=STRING
;

GroupedView: 
	group+=AdvancedType ('+' group+=AdvancedType)*
;

Variable: 
	'var' name=ID (value=Type | value=AdvancedType)
;

Type: 	
	Text | Number | Boolean
;

Text returns Type: 
	{Text} 'text' value=TextValue 
;

Number returns Type: 
	{Number} 'number' (value=Whole | value=Decimal)
;

Whole:
	value = INT
;

Decimal:
	value = FLOAT
;

Boolean returns Type: 
	{Boolean} 'bool' value=ID
;

AdvancedType: 
	View | Button | Table 
;

View returns AdvancedType: 
	{View} 'view' '{' contents+= ViewContent*  '}' 
;

Button returns AdvancedType: 
	{Button} 'button' '{' contents += ButtonContent*  '}'
;

DisplayConfiguration:
	ViewConfiguration | CssConfiguration
;

ViewConfiguration: 
	Dimension | Style
;

Dimension returns ViewConfiguration: 
	{Dimension} 'dimensions' values+=DimensionValue+
;

DimensionValue:
	Height | Width
;

Height returns DimensionValue:
	{Height} 'height' value=STRING
;

Width returns DimensionValue:
	{Width} 'width' value=STRING
;

Style returns ViewConfiguration: 
	{Style} 'style' styleDefinitions+=StyleDefinition+
;

StyleDefinition: 
	'bold' | 'italic' | 'underline' | 'float' 
;

CssConfiguration:
	CssClass | CssId
;

CssClass returns CssConfiguration: 
	{CssClass} 'cssclass' value=STRING
;

CssId returns CssConfiguration: 
	{CssId} 'cssid' value=STRING
;

ButtonContent: 
	Clicker | DisplayConfiguration | Text
;

Clicker: 
	{Click} 'click' '->'  click = (IfElse |Action)
;

IfElse: 
	'if' condition=LogicExp '{' ifAction = Action '}' ( 'else' '{' elseAction = Action '}')?
;

LogicExp returns Proposition:
	Conjunction ('OR' {Disjunction.left=current} right=Conjunction)* 
;

Conjunction returns Proposition:
	Condition ('AND' {Conjunction.left=current} right=Condition)*
;

Condition returns Proposition:
	Comparison | LogParenthesis
;

Comparison:
	left=Exp (op=RelationalOp right=Exp)?
;

RelationalOp:
	'==' | '!=' | '<' | '>' | '<=' | '>=' 
;

LogParenthesis:
	'[' condition=LogicExp ']'
;

Exp returns Expression:
	Factor ( ('+' {Add.left=current} | '-' {Sub.left=current}) right=Factor)*
;

Factor returns Expression:
	Primitive ( ('*' {Mul.left=current} | '/' {Div.left=current}) right=Primitive)* 
;

Primitive returns Expression:
	Name | NumberValue | BooleanValue | Index | ExpParenthesis
;

Index returns Index:
	{Index} value = 'current' | value = 'last'
;

Name: 
	name = [Variable]  
;

NumberValue:
	Whole | Decimal
;

BooleanValue:
	value='true' | value='false'
;

ExpParenthesis:
	'(' expression=Exp ')'
;

Action: 
	 {Action} (pageDirection=PageDirection | alert=Alert | script=Script | advancedType=AdvancedType | include = Include) 
;

PageDirection:
	'next' | 'previous' | 'first' | 'last' | 'goto' STRING
;

Script: 
	name=ID '(' ((parameters+=TextValue)','?)*  ')'
;

Alert: 
	'alert' value=TextValue
;

Editable:
	'editable' value=STRING
;

ViewContent: 
	DisplayConfiguration | Text | Image | Include | Editable  | GroupedView
;

Image: 
	'image' value=STRING 
;

Table: 
	{Table} 'table' '{' contents += TableContent* '}'
;

TableContent: 
	DisplayConfiguration | TableDimension | Header | Row 
;

TableDimension: 
	'tabledimensions' value=STRING
;

Header: 
	'header' contents+=TextValue+
;

Row: 
	'row' contents+=TextValue+
;

Include: 
  	{Include}	'include' include=[Variable]
;

TextValue:
	{TextValue} string=STRING | variable=[Variable]
;

@Override
terminal SL_COMMENT: '#' !('\n'|'\r')* ('\r'? '\n')?;
terminal FLOAT:  INT'.'INT;